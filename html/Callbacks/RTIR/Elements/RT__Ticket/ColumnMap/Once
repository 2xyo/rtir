<%INIT>
use RT::Crypt::GnuPG;

# RTIR's custom fields have _RTIR_ prefix and we should delete
# that on display. However in RT 3.8.8 we moved CustomFields from
# Tickets' map to generic
if ( $COLUMN_MAP->{'CustomField'} ) {
    my $rtir_cfs = RT::CustomFields->new( $RT::SystemUser );
    $rtir_cfs->Limit( FIELD => 'Name', OPERATOR => 'STARTSWITH', VALUE => '_RTIR_' );
    while ( my $cf = $rtir_cfs->Next ) {
        my $name = $cf->Name;
        my ($display_name) = $name =~ /^_RTIR_(.*)/i;
        next if exists $COLUMN_MAP->{ "CustomField.{$name}" };

        my %h = (
            title => $display_name,
            value => sub {
                return $COLUMN_MAP->{ "CustomField" }->{'value'}->(@_, $name)
            },
        );
        $COLUMN_MAP->{ "CF.{$name}" } =
            $COLUMN_MAP->{ "CustomField.{$name}" } = \%h;
    }
}

$COLUMN_MAP->{'Take'} = {
    title => 'Take',
    value => sub {
        my $t = shift;
        return '' if $t->Owner != $RT::Nobody->id;
        my $action = 'Take';
        my $link = RT->Config->Get('WebPath') ."/Ticket/Display.html?Action=$action&id=". $t->id;
        $link = qq{<a href="$link">}. $t->loc($action) .qq{</a>};
        return \$link;
    },
};
$COLUMN_MAP->{'TakeOrSteal'} = {
    title => 'Take/Steal',
    value => sub {
        my $t = shift;
        return '' if $t->Owner == $t->CurrentUser->id;
        my $action = 'Take';
        $action = 'Steal' if $t->Owner != $RT::Nobody->id;
        my $link = RT->Config->Get('WebPath') ."/Ticket/Display.html?Action=$action&id=". $t->id;
        $link = qq{<a href="$link">}. $t->loc($action) .qq{</a>};
        return \$link;
    },
};
$COLUMN_MAP->{'HasIncident'} = {
    title => 'Has Incident?',
    value => sub {
        my $t = shift;
        my $yesno;

        my $query = "Queue = 'Incidents' AND HasMember = " . $t->Id . " AND CF.{State} != 'rejected'";
        my $incidents = new RT::Tickets($session{'CurrentUser'});
        $incidents->FromSQL($query);

        if ($incidents->Count > 0) {
            $yesno = '<span class="yes">yes</span>';
        }
        else {
            $yesno = '<span class="no">no</span>';
        }

        return \$yesno;
    },
};

</%INIT>
<%ARGS>
$COLUMN_MAP => undef
</%ARGS>
