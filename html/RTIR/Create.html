%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2003 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& /RTIR/Elements/Header,
    Title => $Title,
    onload => "function () { hide('Ticket-Create-details') }",
&>
<& /Elements/Tabs &>

% $m->callback(CallbackName => 'BeforeActionList', %ARGS, Actions => \@results, ARGSRef => \%ARGS, Ticket => $SplitObj);
<& /Elements/ListActions, actions => \@results &>

% if ( $Split && !$SplitObj->CurrentUserHasRight('ModifyTicket') ) {
<% loc("You are not allowed to split this [_1].", $name) %>
%    $m->abort();
% }

% unless ( $Split ) {
<form action="<% RT->Config->Get('WebPath') %>/RTIR/Create.html" method="post" enctype="multipart/form-data">
% } else {
<form action="<% RT->Config->Get('WebPath') %>/RTIR/Split.html" method="post" enctype="multipart/form-data">
% }
<input type="hidden" name="id" value="new" />
<input type="hidden" name="Queue" value="<% $Queue %>" />
% if ( $Split ) {
<input type="hidden" name="Split" value="<% $SplitObj->id %>" />
% }

<a name="top"></a>

% if ( $gnupg_widget ) {
<& /Elements/GnuPG/SignEncryptWidget:ShowIssues, self => $gnupg_widget &>
% }

<div id="Ticket-Create-basics">
<a name="basics"></a>

<div id="ticket-create-metadata">
<&| /Widgets/TitleBox, title => loc("Basics"), class=>'ticket-info-basics' &>
<table width="100%" border="0">

% if ( $Incident ) {
  <tr class="incident">
    <td class="label"><&|/l&>Incident</&>:</td>
    <td class="value"><% $Incident %><input type="hidden" name="Incident" value="<% $Incident || '' %>" /></td>
  </tr>
% } elsif ( $SplitObj ) {
  <tr class="split">
    <td class="label"><&|/l&>Split from</&>:</td>
    <td class="value"><% $SplitObj->id %></td>
  </tr>
% if ( $parentvalue ) {
  <tr class="incident">
    <td class="label"><&|/l&>Will be linked to Incident(s)</&>:</td>
    <td class="value"><% $parentvalue %></td>
  </tr>
% }
% } else {
  <tr class="incident">
    <td class="label"><&|/l&>Incident</&>:</td>
    <td class="value"><input type="text" name="Incident" size="16" maxsize="16" value="<% $Incident || '' %>" /></td>
  </tr>
  <tr>
    <td class="label">&nbsp;</td>
    <td class="comment">
        (<&|/l&>Id of an Incident to link a new ticket to</&>. <% $Type eq 'Block'? '<strong>'.loc('Required').'</strong>': loc('Optional') |n %>.)
    </td>
  </tr>
% }

  <& /Ticket/Elements/EditBasics,
    InTable => 1,
    fields  => [
        {
            name => 'Status',
            comp => '/Elements/SelectStatus',
            args => {
                Name         => "Status",
                Default      => $ARGS{Status} || $QueueObj->Lifecycle->DefaultOnCreate,
                DefaultValue => 0,
                SkipDeleted  => 1,
                QueueObj     => $QueueObj,
            },
        },
        {
            name => 'Owner',
            comp => '/Elements/SelectOwner',
            args => {
                Name            => "Owner",
                Default         => $ARGS{Owner} || $session{'CurrentUser'}->id || RT->Nobody->Id,
                DefaultValue    => 0,
                QueueObj        => $QueueObj,
            },
        },
    ],
  &>

% if ( $constituency_cf ) {
  <tr>
    <td class="label"><&|/l&>Constituency</&>:</td>
    <td class="value">
% if ( !$Incident || RT->Config->Get('_RTIR_Constituency_Propagation') eq 'no' ) {
% my $default;
% $default = $IncidentObj->FirstCustomFieldValue('Constituency') if $Incident;
% $default ||= RT::IR::DefaultConstituency( $QueueObj );
% $default ||= scalar RT->Config->Get('RTIR_CustomFieldsDefaults')->{'Constituency'};
      <& /RTIR/Elements/EditRTIRField, 
         QueueObj => $QueueObj, 
         Name     => 'Constituency',
         Default  => $default &>
% } else {
% #TODO: hacks, we need way to define CF value on Create, but don't allow user to change it
      <input type="hidden" class="hidden" name="<% $constituency_field %>-Magic" value="1" />
      <input type="hidden" class="hidden" name="<% $constituency_field %>" value="<% $IncidentObj->FirstCustomFieldValue('Constituency') %>" />
      <& /RTIR/Elements/ShowRTIRField, Ticket => $IncidentObj, Name => 'Constituency' &>
% }
    </td>
  </tr>
% }

  <& /RTIR/Elements/EditCustomFields, 
    %ARGS,
    TicketOnj => undef,
    QueueObj => $QueueObj,
    DefaultsFrom => $SplitObj || $IncidentObj,
  &>
</table></&></div>

<div id="ticket-create-message">
<&| /Widgets/TitleBox, title => $Title, class => 'messagedetails' &>
<table width="100%" border="0">

  <tr>
    <td class="label"><&|/l&>Correspondents</&>:</td>
    <td class="value"><& /Elements/EmailInput,
        Name => 'Requestors', Default => $ARGS{'Requestors'}, Size => undef,
    &></td>
  </tr>
  <tr>
    <td class="label">&nbsp;</td>
    <td class="comment">
      <input type="checkbox" name="SkipNotification" value="Requestors" <% (grep $_ eq 'Requestors', @SkipNotification)? 'checked': '' %> />
      <&|/l&>Don't send any emails to correspondents.</&>
    </td>
  </tr>
% $m->callback( CallbackName => 'AfterRequestors', ARGSRef => \%ARGS );

  <tr>
    <td class="label"><&|/l&>Cc</&>:</td>
    <td class="value"><& /Elements/EmailInput,
        Name => 'Cc', Default => $ARGS{'Cc'}, Size => undef,
    &></td>
  </tr>
  <tr>
    <td class="label">&nbsp;</td>
    <td class="comment"><i><font size="-2">
        <&|/l&>(Sends a carbon-copy of this update to a comma-delimited list of email addresses. These people <b>will</b> receive future updates.)</&>
    </font></i></td>
  </tr>

  <tr>
    <td class="label"><&|/l&>Admin Cc</&>:</td>
    <td class="value"><& /Elements/EmailInput,
        Name => 'AdminCc', Default => $ARGS{'AdminCc'}, Size => undef,
    &></td>
  </tr>
  <tr>
    <td class="label">&nbsp;</td>
    <td class="comment"><i><font size="-2">
        <&|/l&>(Sends a carbon-copy of this update to a comma-delimited list of administrative email addresses. These people <b>will</b> receive future updates.)</&>
    </font></i></td>
  </tr>

  <tr>
    <td class="label"><&|/l&>Subject</&>:</td>
    <td class="value"><input type="text" name="Subject" maxsize="200" value="<% $Subject || '' %>" /></td>
  </tr>

% if ( $gnupg_widget ) {
  <tr><td>&nbsp;</td><td class="value">
  <& /Elements/GnuPG/SignEncryptWidget, self => $gnupg_widget, QueueObj => $QueueObj &>
  </td></tr>
% }

  <tr>
    <td class="label"><&|/l&>Message</&>:</td>
    <td class="value">
% my $include_signature = 1;
% unless (exists $ARGS{Content}) {
%   my $Transactions;
%   if ($SplitObj) {
%     $Transactions = $SplitObj->Transactions;
%   } elsif ($IncidentObj) {
%     $Transactions = $IncidentObj->Transactions;
%   }
%
%   if ($Transactions) {
%   # Investigations should quote their included text
%   my $quote = 0;
%   if ($Type eq 'Investigation') {
%       $quote = 1;
%   }
%   $ARGS{Content} = $m->scomp("/RTIR/Elements/TransactionData", 
%      Transactions => $Transactions,
%      Type => 'messages',
%      Include => {'Create', 'Correspond'},
%      QuoteText => $quote);
%      $ARGS{Content} =~ s/\&gt;/>/g;
%   }
% } else {
%   $include_signature = 0;
% }
% $m->callback( %ARGS, CallbackPage => '/Ticket/Create.html', CallbackName => 'BeforeMessageBox' );
% if ( $ARGS{Content} ) {
<& /Elements/MessageBox, Default => $ARGS{Content}, IncludeSignature => $include_signature &>
% } else {
<& /Elements/MessageBox, QuoteTransaction => $QuoteTransaction &>
% }
    </td>
  </tr>

  <& /Ticket/Elements/AddAttachments, %ARGS, QueueObj => $QueueObj &>

</table></&>

<& /Elements/Submit, Name => "Create", Label => $SubmitCaption &>
</div></div>

<div id="Ticket-Create-details">
<a name="details"></a>


<&| /Widgets/TitleBox, title => loc("Dates"), class=>'ticket-info-dates' &>
<table width="100%" border="0">
<& /Ticket/Elements/EditBasics,
    InTable => 1,
    fields  => [
        {
            name => 'Starts',
            comp => '/Elements/SelectDate',
            args => { Name => 'Starts', Default => $ARGS{'Starts'} || '' },
        },
        {
            name => 'Due',
            comp => '/Elements/SelectDate',
            args => { Name => 'Due', Default => $ARGS{'Due'} || '' },
        },
    ],
&>
</table></&>

<&| /Widgets/TitleBox, title => loc("Time"), class=>'ticket-info-time' &>
<table width="100%" border="0">
<& /Ticket/Elements/EditBasics,
    InTable => 1,
    fields  => [
        {
            name => 'Time Estimated',
            comp => '/Elements/EditTimeValue',
            args => {
                Name    => "TimeEstimated",
                Default => $ARGS{'TimeEstimated'} || '',
            },
        },
        {
            name => 'Time Worked',
            comp => '/Elements/EditTimeValue',
            args => {
                Name    => "TimeWorked",
                Default => $ARGS{'TimeWorked'} || '',
            },
        },
        {
            name => 'Time Left',
            comp => '/Elements/EditTimeValue',
            args => {
                Name    => "TimeLeft",
                Default => $ARGS{'TimeLeft'} || '',
            },
        },
    ],
&>
</table></&>

% if ( $Type eq "Investigation" && $IncidentObj ) {
  <& /RTIR/Elements/AttachReports, Ticket => $IncidentObj &>
% }

</div>

</form>

<%INIT>
$m->callback(
    CallbackName => 'Init', CallbackPage => '/Ticket/Create.html',
    ARGSRef => \%ARGS,
);

my $QueueObj = new RT::Queue( $session{'CurrentUser'} );
$QueueObj->Load( $Queue ) || Abort( loc("Queue could not be loaded.") );


my $Type = RT::IR::TicketType( Queue => $Queue );
if ($Type eq 'Block' && RT->Config->Get('RTIR_DisableBlocksQueue') ) {
    Abort(loc("Blocks queue is disabled via config file"));
}
if ( $m->comp_exists("/RTIR/$Type/Create.html") ) {
    return $m->comp( "/RTIR/$Type/Create.html", %ARGS );
}

my ($name);
if ( $Type eq 'Report' ) {
    $name = 'Incident Report';
}
$name ||= $Type;

my (@results, $checks_failure, $skip_create);
$skip_create = !$ARGS{'Create'};

my $constituency_cf = RT::IR->CustomFields( 'Constituency', Queue => $Queue );
my $constituency_field;
$constituency_field =
    'Object-RT::Ticket--CustomField-' . $constituency_cf->id .'-Values'
        if $constituency_cf;

my $IncidentObj;
if ( $Incident ) {
    $IncidentObj = RT::Ticket->new( $session{'CurrentUser'} );
    $IncidentObj->Load( $Incident );
    unless ( $IncidentObj->id ) {
        push @results, loc("Couldn't load incident #[_1]", $Incident );
        ($checks_failure, $IncidentObj, $Incident) = (1, undef, undef);
    }
    elsif ( $IncidentObj->QueueObj->Name ne 'Incidents' ) {
        push @results, loc('Ticket #[_1] is not an Incident', $Incident );
        ($checks_failure, $IncidentObj, $Incident) = (1, undef, undef);
    }
    elsif ( !$skip_create && $constituency_field 
        && RT->Config->Get('_RTIR_Constituency_Propagation') eq 'reject'
        && lc $ARGS{$constituency_field} ne lc $IncidentObj->FirstCustomFieldValue('Constituency') )
    {
        push @results, loc('Incident #[_1] has constituency [_2], choose the same value for a new ticket or use another Incident', $Incident, $IncidentObj->FirstCustomFieldValue('Constituency') );
        ($checks_failure, $IncidentObj, $Incident) = (1, undef, undef);
    }
    else {
        $Subject ||= $IncidentObj->Subject;
    }
}
$Incident = $ARGS{'Incident'} = $IncidentObj->id if $IncidentObj;

my $SplitObj;
if ( $Split ) {
    $SplitObj = LoadTicket( $SplitObj );
    $ARGS{'Split'} = $Split = $SplitObj->id;
}

my $parentvalue = '';
if ( $IncidentObj ) {
    $parentvalue = $IncidentObj->Id;
} elsif ( $SplitObj ) {
    my $incidents = RT::IR->Incidents( $SplitObj );
    while ( my $incident = $incidents->Next ) {
        $parentvalue .= ' ' if $parentvalue;
        $parentvalue .= $incident->Id;
    }
}

push @results, $m->comp( 'SELF:ProcessAttachments', %ARGS );

my $gnupg_widget = $m->comp('/Elements/GnuPG/SignEncryptWidget:new', Arguments => \%ARGS );
$m->comp( '/Elements/GnuPG/SignEncryptWidget:Process',
    self      => $gnupg_widget,
    QueueObj  => $QueueObj,
);
if ( !$skip_create && $gnupg_widget ) {
    my $status = $m->comp('/Elements/GnuPG/SignEncryptWidget:Check',
        self      => $gnupg_widget,
        Operation => 'Create',
        QueueObj  => $QueueObj,
    );
    $checks_failure = 1 unless $status;
}

unless ( $skip_create ) {
    if ( $Type eq 'Investigation' && !$ARGS{'Requestors'} ) {
        push @results, loc( "You must enter a correspondent" );
        $checks_failure = 1;
    } elsif ( $Type eq 'Block' and not $IncidentObj ) {
        push @results, loc( "You must enter an Incident ID" );
        $checks_failure = 1;
    }

    my $TicketCFs = $QueueObj->TicketCustomFields;
    my ($status, @msg) = $m->comp('/Elements/ValidateCustomFields',
        CustomFields => $TicketCFs,
        ARGSRef      => \%ARGS,
    );
    unless ( $status ) {
        push @results, loc( "Invalid value(s) of the custom fields:" );
        while (my $CF = $TicketCFs->Next) {
            my $msg = $m->notes('InvalidField-' . $CF->Id) or next;
            push @results, $CF->Name . ': ' . $msg;
        }
        $checks_failure = 1;
    }
}

# for RTFM and other things
$m->callback(
    CallbackName => 'BeforeCreate', CallbackPage => '/Ticket/Create.html',
    ARGSRef => \%ARGS, results => \@results,
    skip_create => \$skip_create, checks_failure => $checks_failure
);

unless ( $skip_create || $checks_failure ) {
    $ARGS{'new-MemberOf'} = $parentvalue if $parentvalue;
    $m->callback(CallbackName => 'BeforeDisplay', Ticket => $SplitObj, ARGSRef => \%ARGS);
    push @{ $session{'Actions'}{''} ||= [] }, @results;
    return $m->comp( 'Display.html', %ARGS, SkipNotification => \@SkipNotification );
}

unshift @results, loc("Creation failed:")
    if $checks_failure && @results;

my ($Title, $SubmitCaption);
if ( $Split ) {
    $Title = loc("Split [_1] #[_2]: [_3]", $name, $SplitObj->id, $SplitObj->Subject);
    $SubmitCaption = loc("Split");
} else {
    if ( $Type eq 'Investigation' ) {
        $Title = loc("Launch a new [_1]", $name);
        $SubmitCaption = loc("Launch");
    } else {
        $Title = loc("Create a new [_1]", $name);
        $SubmitCaption = loc("Create");
    }
}

PageMenu->child( basics => raw_html =>  q[<a href="#basics" onclick="return switchVisibility('Ticket-Create-basics','Ticket-Create-details');">] . loc('Basics') . q[</a>]);
PageMenu->child( details => raw_html =>  q[<a href="#details" onclick="return switchVisibility('Ticket-Create-details','Ticket-Create-basics');">] . loc('Details') . q[</a>]);

</%INIT>

<%ARGS>
$Queue            => undef
@SkipNotification => ()

$Subject          => undef
$QuoteTransaction => undef

# Parent Incident
$Incident         => undef

$Split            => undef
</%ARGS>

<%METHOD ProcessAttachments>
<%ARGS>
@DeleteAttachments => ()
$Attachment => '';
</%ARGS>
<%INIT>

my @results;

# deal with deleting uploaded attachments
foreach (
    grep $_ && exists $session{'Attachments'}{ $_ },
    @DeleteAttachments,
    map { /^DeleteAttach-(.*)$/; $1 } keys %ARGS,
) {
    delete $session{'Attachments'}{ $_ };
    push @results, loc("Deleted attachment '[_1]'", $_ );
    $session{'i'}++;
}

# store the uploaded attachment in session
foreach my $field ( 'Attach', 'Attachment' ) {
    next unless $ARGS{ $field };

    my $filename = "$ARGS{ $field }";
    $filename =~ s{^.*[\\/]}{};

    my $entity = MakeMIMEEntity(
        Subject             => $filename,
        Body                => '',
        AttachmentFieldName => $field,
    );
    $session{'Attachments'}{ $filename } = $entity;
    push @results, loc("Added attachment '[_1]'", $filename );
    $session{'i'}++;
}

delete $session{'Attachments'} if $session{'Attachments'} && !%{ $session{'Attachments'} };

return @results;
</%INIT>
</%METHOD>
