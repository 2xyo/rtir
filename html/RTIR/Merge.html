%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2003 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& /Elements/Header, Title => $title, Refresh => $session{'tickets_refresh_interval'} &>

<& '/RTIR/Search/Elements/RefineTabs',
    Title          => $title,
    Ticket         => $Ticket,
    Queue          => $Queue,
    BaseQuery      => $BaseQuery,
    QueryString    => $QueryString,

    current_tab    => $current_tab,
    current_subtab => 'RTIR/Search/Refine.html',
&>

<& /Elements/ListActions, actions => \@results &>

<form action="<% RT->Config->Get('WebPath') %>/RTIR/Merge.html" method="post">
<input type="hidden" name="id" value="<% $id %>" />

% if ( $siblings_query ) {
<& /RTIR/Elements/MergeWith,
    %defaults,
    %ARGS,
    Title     => loc('Other children of the parent incident(s) of #[_1]', $id),
    Ticket    => $Ticket,
    BaseQuery => "id != $id",
    Query     => $siblings_query,
&>
% }

<& /RTIR/Elements/MergeWith, %defaults, %ARGS, Queue => $Ticket->QueueObj->Name, Ticket => $Ticket &>

% if ( $Type eq 'Report' ) {
<& /RTIR/Elements/MergeWith, %defaults, %ARGS, Queue => "Investigations", Ticket => $Ticket &>
% } elsif ( $Type eq 'Investigation' ) {
<& /RTIR/Elements/MergeWith, %defaults, %ARGS, Queue => "Incident Reports", Ticket => $Ticket &>
% }
<& /Elements/Submit, Caption=> loc('Merge into selected Ticket'), Label => loc("Merge") &>
</form>

<%INIT>
my $Ticket = LoadTicket($id);
$id = $ARGS{'id'} = $Ticket->id;

my (@results);
if ( $SelectedTicket ) {
    my $MergeTicket = LoadTicket( $SelectedTicket );

    my @queues = sort map {$_->Name} $Ticket->QueueObj, $MergeTicket->QueueObj;
    if ( $queues[0] eq $queues[1] or ($queues[0] eq "Incident Reports" and $queues[1] eq "Investigations")) {
        if ($MergeTicket->QueueObj->Name eq "Incident Reports") {
            # We always want to merge into the Investigation
            ($Ticket, $MergeTicket) = ($MergeTicket, $Ticket);
        }
        
        $ARGS{ $Ticket->id .'-MergeInto' } = $MergeTicket->id;            

        for my $t ( $Ticket, $MergeTicket ) {
            if ($t->Owner != $t->CurrentUser->Id) {
                my ($status, $msg) = $t->SetOwner($t->CurrentUser->Id, 'Force');
                push @results, $msg if $msg;
            }
        }
        push @results, ProcessTicketLinks( TicketObj => $Ticket, ARGSRef => \%ARGS);

        $Ticket = LoadTicket($id);
        $id = $ARGS{'id'} = $Ticket->id;
    } else {
        push @results, loc("Merge failed: Ticket #[_1] is not the right type", $MergeTicket->Id );
    }
}

my ($Type) = $m->comp( '/RTIR/Elements/Type', TicketObj => $Ticket );
my $Queue = $Ticket->QueueObj->Name;

# XXX: clear radio, checkboxes from format
$Format =~ s/__RTIR_(?:Radio|Check)__,//;

my $title = loc("Merge [_1] #[_2]: [_3]", $Type, $id, $Ticket->Subject);

$Query ||= $m->comp('/RTIR/Elements/NewQuery', Queue => $Queue);
unless ( $BaseQuery ) {
    $BaseQuery = $m->comp('/RTIR/Elements/BaseQuery', Queue => $Queue);
    $BaseQuery = join ' AND ', map "( $_ )", grep $_, $BaseQuery, " id != $id";
}


my %defaults = (
    Format  => $Format,
    Rows    => $Rows,
    Page    => $Page,
    OrderBy => $OrderBy,
    Order   => $Order,
);

my $siblings_query = '';
if ( $Type ne 'Incident' ) {
    my $parents = RT::Tickets->new( $session{'CurrentUser'} );
    $parents->FromSQL( "Queue = 'Incidents' AND HasMember = $id" );
    if ( $parents->Count ) {
        my $parents_query = '';
        while ( my $parent = $parents->Next ) {
            $parents_query .= ' OR ' if $parents_query;
            $parents_query .= "MemberOf = ". $parent->id;
        }
        if ( $parents_query ) {
            $siblings_query .= "( $parents_query )";
            my $queue_query = "Queue = '$Queue'";
            $queue_query .= " OR Queue = 'Investigations'" if $Type eq 'Report';
            $queue_query .= " OR Queue = 'Incident Reports'" if $Type eq 'Investigation';
            $siblings_query .= " AND ( $queue_query )";
        }
    }
}

my $QueryString = $m->comp('/Elements/QueryString', Query => $Query, %defaults);

my $current_tab = "RTIR/Merge.html?id=$id";

</%INIT>
      
<%ARGS>
$id             => undef

$BaseQuery      => undef
$Query          => undef
$Format         => RT->Config->Get('RTIRSearchResultFormats')->{'Merge'} 
$Rows           => 50
$Page           => 1
$OrderBy        => 'id'
$Order          => 'ASC'

$SelectedTicket => undef
</%ARGS>
