%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& '/RTIR/Elements/Header', Title => $title &>
<& '/RTIR/Search/Elements/RefineTabs',
   path => $path,
   Queue => $Queue,
   current_tab => $current_tab, 
   Title => $title,
   Format => $Format,
   Query => $Query,
   Rows => $Rows,
   OrderBy => $OrderBy,
   Order => $Order,
   QueryString => $QueryString,
&>

<& /Elements/ListActions, actions => \@results &>

<form action="<%$RT::WebPath%>/RTIR/Report/BulkReject.html" method="POST">
<input type="hidden" name="Status" value="<% $Status %>">

% if( $session{'tickets'}->Count ) {
<a href="BulkReject.html?RTIRCheck=All">[Select All]</a>
<a href="BulkReject.html?RTIRCheck=None">[Unselect All]</a>
% }

<& /RTIR/Search/Elements/ShowResults,
    Query => $Query,
    QueryString => $QueryString,
    BaseURL => $BaseURL,
    Format => $Format,
    RTIRCheck => $RTIRCheck,
&>

% if( $session{'tickets'}->Count ) {
<a href="BulkReject.html?RTIRCheck=All">[Select All]</a>
<a href="BulkReject.html?RTIRCheck=None">[Unselect All]</a>
<& /Elements/Submit, Name => "BulkReject", Caption => $SubmitCaption, Label => loc("Reject") &>
% }

</form>

<%INIT>
my $title         = loc("Reject Incident Reports");
my $SubmitCaption = loc("Reject selected incident reports");

$Format         ||= $RT::RTIRSearchResultFormats->{'RejectReports'};
unless( $Format =~ /__RTIR_Check__/ ) {
    $Format = "___RTIR_Check__,$Format";
}

my ( @results );
if ( $ARGS{'BulkReject'} ) {
    my @tempresults;

    #Iterate through each ticket we've been handed
    foreach my $id( @SelectedTickets ) {
        $m->out('<!--  Processing <%$id%> -->');
        $m->flush_buffer;

        my $t = RT::Ticket->new( $session{'CurrentUser'} );
        $t->Load( $id );
        unless( $t->id ) {
            push @tempresults, [ $id, loc("Couldn't load ticket.") ];
            push @tempresults, [ $id, loc("Skipped.") ];
            next;
        }
        $id = $t->id;

        # If we don't own the thing we're linking to, change the owner
        if (   $t->Owner != $session{'CurrentUser'}->id
            && $t->Owner != $RT::Nobody->id )
        {
            push @tempresults, [ 
                $id,
                loc("You may not reject tickets that belong to another user."),
            ];
            push @tempresults, [ $id, loc("Skipped.") ];
            next;
        }

        my $oldstate = $t->FirstCustomFieldValue('_RTIR_State');
        
        if ( $t->Owner != $session{'CurrentUser'}->id &&
             $t->Owner == $RT::Nobody->id )
        {
            my ( $res, $msg ) = $t->Take;
            unless( $res ) {
                push( @tempresults, [ $id, loc("Couldn't take ticket: [_1]", $msg) ] );
            } else {
                push( @tempresults, [ $id, loc("Took ticket. [_1]", $msg) ] );
            }
        }

        push @tempresults, ProcessTicketBasics( ARGSRef => \%ARGS, TicketObj => $t );

        my $newstate = $t->FirstCustomFieldValue('_RTIR_State');
        if ( $newstate ne $oldstate ) {
            push @tempresults,
                 [ $id, loc("State changed from [_1] to [_2]", $oldstate, $newstate ) ];
        }
    }
    push @results, map { ref($_)? loc( "Ticket [_1]: [_2]", $_->[0], $_->[1] ): $_ }
                   @tempresults;
    # force redo search if we changed state
    $session{'tickets'}->RedoSearch;
}

my $QueryString = $m->comp(
    '/Elements/QueryString',
    BaseQuery => $BaseQuery,
    Query     => $Query,
    Format    => $Format,
    Rows      => $Rows,
    OrderBy   => $OrderBy,
    Order     => $Order,
    Page      => $Page
);

$Query = "$BaseQuery AND ( $Query )";
my $path = "RTIR/Search/Refine.html?$QueryString&";
$path .= $m->comp( '/Elements/QueryString',
                   ResultPage  => $BaseURL,
                   current_tab => $current_tab,
                   Queue       => $Queue,
                 );

</%INIT>
<%ARGS>
$Queue           => 'Incident Reports'

# FIXME: Doesn't work as expected with refined search.
# also is not clear should it use Query from
# /RTIR/Search/Results.html?Queue=Incident%20Reports or 
# own? and can it be refined at all?
$BaseQuery       => $m->comp( '/RTIR/Elements/BaseQuery', Queue => $Queue )
$Query           => $m->comp( '/RTIR/Elements/NewQuery',  Queue => $Queue )
$Status          => 'rejected'
$RTIRCheck       => 'None'
$Format          => undef
$Rows            => 50
$Page            => 1
$OrderBy         => 'id'
$Order           => 'ASC'
$BaseURL         => $RT::WebPath . "/RTIR/Report/BulkReject.html?"
$current_tab     => 'RTIR/Report/BulkReject.html'
@SelectedTickets => ()
</%ARGS>
