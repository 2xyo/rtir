%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& /RTIR/Elements/Header, Title => $title, Refresh => $session{'tickets_refresh_interval'} &>
<& /RTIR/Search/Elements/RefineTabs, 
    Title          => $title,

    Queue          => 'Incident Reports',
    Ticket         => $IncidentObj,
    BaseQuery      => "( $IRBaseQuery ) OR ( $InvBaseQuery ) OR ( $BlockBaseQuery )",
    QueryString    => $QueryString,

    current_tab    => $current_tab,
    current_subtab => "RTIR/Search/Refine.html",
&>

<& /Elements/ListActions, actions => \@results &>

<form action="Reply.html" method="get">
<input type="hidden" name="id" value="<% $id %>" />
<input type="hidden" name="DefaultStatus" value="<% $DefaultStatus %>" />
<input type="hidden" name="Status" value="<% $DefaultStatus %>" />
<input type="hidden" name="All" value="<% $All %>" />
<input type="hidden" name="Query" value="<% $Query %>" />

<h2><&|/l&>Reporters</&></h2>
<& /RTIR/Search/Elements/ShowResults,
    Queue     => 'Incident Reports',
    BaseURL   => $current_tab,
    BaseQuery => $IRBaseQuery,
    Query     => $Query,
    Format    => '__RTIR_Check__,'. $Format,
    Rows      => $Rows,
    Page      => $Page,
    OrderBy   => $OrderBy,
    Order     => $Order,
&>

% if ( $All ) {
<h2><&|/l&>Investigation Correspondents</&></h2>
<& /RTIR/Search/Elements/ShowResults,
    Queue     => 'Investigations',
    BaseURL   => $current_tab,
    BaseQuery => $InvBaseQuery,
    Query     => $Query,
    Format    => '__RTIR_Check__,'. $Format,
    Rows      => $Rows,
    Page      => $Page,
    OrderBy   => $OrderBy,
    Order     => $Order,
&>
<h2><&|/l&>Blocks Correspondents</&></h2>
<& /RTIR/Search/Elements/ShowResults,
    Queue     => 'Investigations',
    BaseURL   => $current_tab,
    BaseQuery => $BlockBaseQuery,
    Query     => $Query,
    Format    => '__RTIR_Check__,'. $Format,
    Rows      => $Rows,
    Page      => $Page,
    OrderBy   => $OrderBy,
    Order     => $Order,
&>
% }

<& Elements/ReplyForm, AllowResponse => ($ARGS{Status} !~ /^(?:resolved|rejected)$/) &>

<& /Elements/Submit, Name => "SubmitTicket", Caption => $SubmitCaption, Label => $SubmitLabel &>
</form>

<%INIT>
my (@results);

my $IncidentObj = LoadTicket( $id );
$id = $ARGS{'id'} = $IncidentObj->id;

# XXX: clear radio, checkboxes from format
$Format =~ s/__RTIR_(?:Radio|Check)__,//;

my $QueryString = $m->comp( '/Elements/QueryString',
                            Query => $Query,
                            Format => $Format,
                            Rows => $Rows,
                            OrderBy => $OrderBy,
                            Order => $Order,
                            Page => $Page);

my ($title, $SubmitCaption, $SubmitLabel);
if ($DefaultStatus eq 'resolved') {
    $title = loc("Resolve Incident #[_1] (including children)", $id);
    $SubmitCaption = loc("Resolve Incident");
    $SubmitLabel = loc("Resolve");
} elsif ($DefaultStatus eq 'rejected') {
    $title = loc("Abandon Incident #[_1] (including children)", $id);
    $SubmitCaption = loc("Abandon Incident");
    $SubmitLabel = loc("Abandon");
} else {
    $SubmitCaption = loc("Reply to selected correspondents");
    $SubmitLabel = loc("Reply");
    if ($All) {
        $title = loc("Incident #[_1]: Reply to All", $id);
    } else {
        $title = loc("Incident #[_1]: Reply to Reporters", $id);
    }
}

my $ActiveStatesQuery;
{
    my @active_states;
    foreach my $q( 'Incident Reports', 'Investigations', 'Blocks' ) {
        push @active_states, $m->comp('/RTIR/Elements/States', Queue => $q);
    }
    my %seen;
    $ActiveStatesQuery = join ' OR ',
        map "CF.{_RTIR_State} = '$_'",
        grep !$seen{ $_ },
        @active_states;
}

unless ( $Query ) {
    $Query = "( $ActiveStatesQuery )";
}

my $ActiveChildrenQuery = join ' AND ', map "( $_ )", grep $_,
    "MemberOf = $id",
    $ActiveStatesQuery,
    join ' OR ',
        map "Queue = '$_'",
        'Incident Reports', 'Investigations', 'Blocks';

$m->comp( '/RTIR/Create.html:ProcessAttachments', %ARGS );

if ( $SubmitTicket ) {
    foreach my $id( @SelectedTickets ) {
        my $Ticket = RT::Ticket->new( $session{'CurrentUser'} );
        $Ticket->Load( $id );
        unless( $Ticket->id ) {
            push @results, loc( "Ticket [_1]: Couldn't load ticket.", $id );
            next;
        }
        $id = $Ticket->id;

        my $oldstate = RT::IR::Ticket::FirstCustomFieldValue( $Ticket, '_RTIR_State' );

        my @tempresults;
        $ARGS{'UpdateAttachments'} = delete $session{'Attachments'};
        push @tempresults, ProcessUpdateMessage( TicketObj => $Ticket, ARGSRef => \%ARGS );

        unless ( RT::IR::Ticket::IsLinkedToActiveIncidents( $Ticket, $IncidentObj ) ) {
            # XXX: we push into results directly cuz ProcessTicketBasics returns results
            # with leading 'Ticket XX:' string
            push @results, ProcessTicketBasics( TicketObj => $Ticket, ARGSRef => \%ARGS );
        }

        my $newstate = RT::IR::Ticket::FirstCustomFieldValue( $Ticket, '_RTIR_State' );

        if ($newstate ne $oldstate) {
            push (@tempresults, loc("State changed from [_1] to [_2]", $oldstate, $newstate));
        }

        push @results, map { loc("Ticket [_1]: [_2]", $id, $_) } @tempresults;
    }

    my $update_incident_state = 1;
    if ( $DefaultStatus =~ /^(rejected|resolved)$/) {
        # if we are going to resolve/reject incident then do it only when we've
        # rejected all children that are not linked to another active incident(s)
        my $children = RT::Tickets->new( $session{'CurrentUser'} );
        $children->FromSQL( $ActiveChildrenQuery );
        while ( my $child = $children->Next ) {
            next if RT::IR::Ticket::IsLinkedToActiveIncidents( $child, $IncidentObj );
            $update_incident_state = 0;
            last;
        }
    }

    if ( $update_incident_state ) {
        my $oldstate = RT::IR::Ticket::FirstCustomFieldValue( $IncidentObj, '_RTIR_State' );
        push @results, ProcessTicketBasics( TicketObj => $IncidentObj, ARGSRef => \%ARGS );
        my $newstate = RT::IR::Ticket::FirstCustomFieldValue( $IncidentObj, '_RTIR_State' );

        if ($newstate ne $oldstate) {
            push @results, loc("State changed from [_1] to [_2]", $oldstate, $newstate);
        }
    } else {
        push @results, loc("State of the Incident wasn't changed, not all children were updated");
    }

    return $m->comp("Display.html", results => \@results, id => $id);
}

my $IRBaseQuery = join ' AND ', map "( $_ )", grep $_,
    $m->comp('/RTIR/Elements/BaseQuery', Queue => 'Incident Reports'), "MemberOf = $id";
my $InvBaseQuery = join ' AND ', map "( $_ )", grep $_,
    $m->comp('/RTIR/Elements/BaseQuery', Queue => 'Investigations'), "MemberOf = $id";
my $BlockBaseQuery = join ' AND ', map "( $_ )", grep $_,
    $m->comp('/RTIR/Elements/BaseQuery', Queue => 'Blocks'), "MemberOf = $id";



my $current_tab = "RTIR/Incident/Reply.html?".
    $m->comp('/Elements/QueryString',
        id            => $id,
        All           => $All,
        DefaultStatus => $DefaultStatus,
        Action        => $Action,
    );

</%INIT>

<%ARGS>
$id              => undef
$All             => 0
$DefaultStatus   => ''
$Action          => 'Correspond'

$Query           => ''
$Format          => RT->Config->Get('RTIRSearchResultFormats')->{'BulkReply'}
$Rows            => 0
$Page            => 1
$OrderBy         => 'id'
$Order           => 'ASC'

@SelectedTickets => ()
$SubmitTicket    => 0
</%ARGS>
