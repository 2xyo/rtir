%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& '/RTIR/Elements/Header', Title => $title &>
<& '/RTIR/Search/Elements/RefineTabs',
    path        => $path,
    Queue       => $Queue,
    current_tab => $current_tab, 
    Title       => $title,
    Format      => $Format,
    Query       => $Query,
    Rows        => $Rows,
    OrderBy     => $OrderBy,
    Order       => $Order,
    QueryString => $QueryString,
&>

<& /Elements/ListActions, actions => \@results &>

<form action="<%RT->Config->Get('WebPath')%>/RTIR/Incident/BulkAbandon.html" method="post">
<input type="hidden" name="Status" value="<% $Status %>" />

<%perl>
# XXX: cache result set to check if get empty results
my $result_set = $m->scomp(
    '/RTIR/Search/Elements/ShowResults',
    Queue       => $Queue,
    BaseQuery   => $BaseQuery,
    Query       => $Query,
    Format      => $Format,
    Rows        => $Rows,
    OrderBy     => $OrderBy,
    Order       => $Order,
    Page        => $Page,
    BaseURL     => $BaseURL,
);
</%perl>

% if( $session{'tickets'}->Count ) {
% $m->out( $result_set );

<& /Widgets/TitleBoxStart, title => loc("Select recipients") &>
% foreach my $box( @CheckBox ) {
% my $checked = (scalar grep $_ && $box->{'Value'}, @ReplyToAll )? ' checked' : '';
<input type="checkbox" name="ReplyToAll" value="<% $box->{'Value'} %>" <% $checked %>" />
<% $box->{'Title'} %><br />
% }
<& /Widgets/TitleBoxEnd &>
<br />

<& Elements/ReplyForm &>

<& /Elements/Submit,
    Name => "BulkAbandon",
    Caption => $SubmitCaption,
    Label => loc("Reject"),
    CheckAll => 1, ClearAll => 1,
    CheckboxName => 'SelectedTickets',
&>
% } else {
<i><% loc("no incidents") %></i>
% }
</form>

<%INIT>
my @CheckBox = (
    { Value => 'Incident Reports', Title => loc("Reply to all reports") },
    { Value => 'Investigations',   Title => loc("Reply to all investigations") },
);
my $title = loc('Abandon Incidents');
my $SubmitCaption = loc("Abandon selected incidents");
$Format = "___RTIR_Check__,$Format" unless $Format =~ /___RTIR_Check__/;

my ( @results );

if ( $ARGS{'BulkAbandon'} ) {
    my @tempresults;

    foreach my $id ( @SelectedTickets ) {
        $m->out('<!--  Processing <%$t->id%> -->'); $m->flush_buffer;

        my $t = RT::Ticket->new( $session{'CurrentUser'} );
        $t->Load( $id );
        unless( $t->id ) {
            push @tempresults, [ $id, loc("Couldn't load ticket.") ];
            next;
        }
        $id = $t->id;

        unless( $t->QueueObj->Name eq 'Incidents' ) {
            push @tempresults, [ $id, loc("Is not incident.") ];
            next;
        }

        # process replies
        if( scalar @ReplyToAll ) {
            # XXX: why do we limit to open?
            my $query = "CF.{_RTIR_State} = 'open' AND MemberOf = $id";
            $query .= " AND (". join(' OR ', map "Queue = '$_'", @ReplyToAll) .")";
            my $members = new RT::Tickets( $t->CurrentUser );
            $members->FromSQL( $query );
            while( my $member = $members->Next ) {
                push @tempresults, ProcessUpdateMessage(TicketObj => $member, ARGSRef => \%ARGS );
            }
        }
        
        # process status updates
        my @tmp;
        my $members = new RT::Tickets( $t->CurrentUser );
        # XXX: do we want limit queues?
        $members->FromSQL( "MemberOf = $id" );
        while( my $member = $members->Next ) {
            push @tmp, $member;
        }

        push @tmp, $t;
        foreach my $t( @tmp ) {
            my $oldstate = RT::IR::Ticket::FirstCustomFieldValue( $t, '_RTIR_State' );

            push @tempresults, ProcessTicketBasics( ARGSRef => \%ARGS, TicketObj => $t );

            my $newstate = RT::IR::Ticket::FirstCustomFieldValue( $t, '_RTIR_State' );
            if ( $newstate ne $oldstate ) {
                push @tempresults,
                     [ $t->id, loc("State changed from [_1] to [_2]", $oldstate, $newstate ) ];
            }
        }
    }

    push @results,
         map { ref($_)? loc( "Ticket [_1]: [_2]", $_->[0], $_->[1] ): $_ }
         @tempresults;

    # force redo search if we changed tickets
    $session{'tickets'}->RedoSearch;
}


# FIXME: Doesn't work as expected with refined search.
# also is not clear should it use Query from
# /RTIR/Search/Results.html?Queue=Incident%20Reports or 
# own? and can it be refined at all?

# We can only reject new tickets, so set the states.
my $QueryString = $m->comp(
    '/Elements/QueryString',
    Query   => $Query,
    Format  => $Format,
    Rows    => $Rows,
    OrderBy => $OrderBy,
    Order   => $Order,
    Page    => $Page
);
$current_tab ||= "RTIR/Incident/BulkAbandon.html?"
    . $m->comp('/Elements/QueryString', Queue => $Queue)
    . "&$QueryString";

my $path = "RTIR/Search/Refine.html?$QueryString&";
$path .= $m->comp( '/Elements/QueryString',
                   ResultPage  => $BaseURL,
                   current_tab => $current_tab,
                   Queue       => $Queue,
                 );

</%INIT>
<%ARGS>
$Status    => 'rejected'
$Queue     => 'Incidents'
$BaseQuery => $m->comp( '/RTIR/Elements/BaseQuery', Queue => $Queue );
$Query     => $m->comp( '/RTIR/Elements/NewQuery', Queue => $Queue, Inactive => 1 );
$Format    => RT->Config->Get('RTIRSearchResultFormats')->{'AbandonIncidents'};
$Rows      => 50
$Page      => 1
$OrderBy   => 'id'
$Order     => 'ASC'
$BaseURL   => RT->Config->Get('WebPath') . "/RTIR/Incident/BulkAbandon.html"
$current_tab => undef
@SelectedTickets => ()
@ReplyToAll => ()
</%ARGS>
