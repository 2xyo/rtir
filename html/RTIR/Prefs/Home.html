<& /Elements/Header, Title => loc("Customize '[_1]'", loc("RTIR home page") ) &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@results &>

<& /Widgets/SelectionBox:header, nojs => 1 &>
% foreach my $box( @boxes ) {
<&|/Widgets/TitleBox,
    title => loc('RTIR at a glance') .': '. loc( $box->{Name} ),
    bodyclass => ""
&>
<& /Widgets/SelectionBox:show, self => $box, nojs => 1 &>
</&>
% }

<%INIT>
# XXX: copy&past of the similar RT's page

my @results;
my $user = $session{'CurrentUser'}->UserObj;

unless (exists $session{'my_rtir_portlets'}) {
    my ($d_portlets) = RT::System->new($session{'CurrentUser'})->Attributes->Named('RTIR_HomepageSettings');
    $session{'my_rtir_portlets'} = $user->Preferences('RTIR_HomepageSettings', $d_portlets->Content);
}
my $portlets = $session{'my_rtir_portlets'};

my %seen;

my @items;
foreach my $comp ( grep !$seen{$_}++, (RT->Config->Get('RTIR_HomepageComponents')) ) {
    my $desc;
    my $obj = $m->fetch_comp($comp);
    $desc = $obj->attr_if_exists('Description') if $obj;
    unless( $desc ) {
        $desc = $comp;
        $desc =~ s/^.*\///;
    }
    push @items, ["component-$comp", $desc];
}

my $sys = RT::System->new($session{'CurrentUser'});
my @objs = ($sys);

push @objs, RT::SavedSearch->new( $session{'CurrentUser'} )->_PrivacyObjects
    if $session{'CurrentUser'}->HasRight( Right  => 'LoadSavedSearch',
                                          Object => $RT::System );

for my $object (@objs) {
    for ($m->comp("/Search/Elements/SearchesForObject", Object => $object)) {
        my ($desc, $search) = @$_;
        my $SearchType = $search->Content->{'SearchType'} || 'Ticket';
        if ($object eq $sys && $SearchType eq 'Ticket') {
            push @items, ["system-$desc", $desc];
        }
        else {
            my $oid = ref($object).'-'.$object->Id.'-SavedSearch-'.$search->Id;
            my $type = ($SearchType eq 'Ticket')
                ? 'Saved Search' : $SearchType; # loc
            push @items, ["saved-$oid", loc($type).": $desc"];
        }
    }
}


my @boxes;
foreach my $pane ( sort keys %$portlets ) {
    push @boxes,
        $m->comp(
            '/Widgets/SelectionBox:new',
            Action    => 'Home.html',
            Name      => $pane,
            Available => \@items,
            AutoSave  => 1,
            OnSubmit  => sub {
                my $self = shift;
                $portlets->{ $pane } = [
                    map { m/(\w+)-(.*)$}/; { type => $1, name => $2, }}
                    @{$self->{Current}}
                ];
                $user->SetPreferences('RTIR_HomepageSettings', $portlets);
                push @results, loc ('Preferences saved for [_1].', $pane);
                delete $session{'my_rtir_portlets'};
            },
            Selected  => [ map { join '-', @{$_}{qw/type name/} } @{ $portlets->{$pane} } ]
        );
}

$m->comp ('/Widgets/SelectionBox:process', %ARGS, self => $_, nojs => 1) for @boxes;

MaybeRedirectForResults( Actions => \@results );
</%INIT>
