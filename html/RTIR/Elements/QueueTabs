%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
% $m->callback( %ARGS, CallbackPage => '/Ticket/Elements/Tabs', Ticket => $Ticket, actions=> $actions, tabs => $tabs);
<& /RTIR/Elements/Tabs,
    %ARGS,
    tabs => $tabs, 
    actions => $actions, 
    current_toptab => $current_toptab,
    current_tab => $mytab,
    current_subtab => $current_subtab,
    Title => $Title,
&> 

<%INIT>
if ( $QueryString =~ s/^&// ) {
    $RT::Logger->warning("query string has leading &");
}

if ( !$Ticket && $id ) {
    $Ticket = RT::Ticket->new( $session{'CurrentUser'} );
    $Ticket->Load( $id );
}
$id = $Ticket->id if $Ticket;

my $Type = RT::IR::TicketType( Queue => $QueueName );

my $EscapedQueueName
    = $m->comp( '/Elements/QueryString', Queue => $QueueName );
my $current_toptab = 'RTIR/Search/Results.html?' . $EscapedQueueName;

my $tabs = {};

my ( $actions, $mytab );
my $searchtabs = {};

if ( $Ticket && $Ticket->QueueObj->Name eq $QueueName ) {
    $mytab = "RTIR/Display.html?id=$id";

    if ( $session{'tickets'} ) {
        my $item_map = $session{'tickets'}->ItemMap;

        # Don't display prev links if we're on the first ticket
        if ( $item_map->{$id}->{'prev'} ) {
            $searchtabs->{'_a'} = {
                class => "nav",
                path  => "RTIR/Display.html?id=" . $item_map->{'first'},
                title => '<< ' . loc('First')
            };
            $searchtabs->{"_b"} = {
                class => "nav",
                path  => "RTIR/Display.html?id=" . $item_map->{$id}->{'prev'},
                title => '< ' . loc('Prev')
            };
        }

        # Don't display next links if we're on the last ticket
        if ( $item_map->{$id}->{'next'} ) {
            $searchtabs->{'d'} = {
                class => "nav",
                path  => "RTIR/Display.html?id=" . $item_map->{$id}->{'next'},
                title => loc('Next') . ' >'
            };
            $searchtabs->{'e'} = {
                class => "nav",
                path  => "RTIR/Display.html?id=" . $item_map->{'last'},
                title => loc('Last') . ' >>'
            };
        }
    }

    $tabs->{'this'} = {
        class => "currentnav",
        path  => "RTIR/Display.html?id=$id",
        title => loc( "$Type #[_1]", $id )
    };

    my %can = map { $_, $Ticket->CurrentUserHasRight($_) }
        qw(ModifyTicket ReplyToTicket OwnTicket CommentOnTicket);


    

    my $ticket_page_tabs = {
        _A => {
            title => loc('Display'),
            path  => "RTIR/Display.html?id=$id"
        }
    };

    # only show edit option if they can do it
    if ( $can{'ModifyTicket'} ) {
        $ticket_page_tabs->{'_E'} = {
            title => loc('Edit'),
            path  => "RTIR/Edit.html?id=$id"
        };

        $ticket_page_tabs->{'_F'} = {
            title => loc('Split'),
            path  => "RTIR/Split.html?Ticket=$id"
        };

        $ticket_page_tabs->{'_G'} = {
            title => loc('Merge'),
            path  => "RTIR/Merge.html?id=$id&$QueryString"
        };

        $ticket_page_tabs->{'_H'} = {
            title     => loc('Advanced'),
            path      => "RTIR/Advanced.html?id=$id",
            separator => 1,
        };

        foreach my $key ( keys %$appendtabs ) {
            foreach ( qw(title path subtabs current_subtab) ) {
                $ticket_page_tabs->{ $key }->{ $_ } = $appendtabs->{ $key }->{ $_ };
            }
        }
    }

    $current_subtab ||= "RTIR/Display.html?id=$id";
    $tabs->{'this'}->{'subtabs'} = $ticket_page_tabs;
    foreach my $tab ( values %$ticket_page_tabs ) {
        next unless $tab->{'path'} eq $current_subtab;

        $tab->{'subtabs'} = { %{ $tab->{'subtabs'} || {} }, %$subtabs } if $subtabs;
        $tabs->{'this'}->{'current_subtab'} = $tab->{'path'};
    }

    ### Reply ###
    if ( $can{'ModifyTicket'} || $can{'ReplyToTicket'} ) {

        if ( $Type eq 'Incident' ) {
            $actions->{'A'} = {
                title => loc('Reply to Reporters'),
                path  =>
                    "RTIR/Incident/Reply.html?id=$id&SelectAllTickets=1",
            };
            $actions->{'Ab'} = {
                title => loc('Reply to All'),
                path  =>
                    "RTIR/Incident/Reply.html?id=$id&SelectAllTickets=1&All=1",
            };
        } elsif ( $Type eq 'Block' ) {
            $actions->{'A'} = {
                title => loc('Reply'),
                path  =>
                    "RTIR/Update.html?id=$id&Action=Respond",
            };
        } else {
            $actions->{'A'} = {
                title => loc('Reply'),
                path  =>
                    "RTIR/Update.html?id=$id&Action=Respond",
            };
        }
    }

    if ( $can{'OwnTicket'} ) {
        if ( $Ticket->Owner == $RT::Nobody->id ) {
            $actions->{'D'} = {
                path  => "RTIR/Display.html?Action=Take&id=$id",
                title => loc('Take')
            } if $can{'ModifyTicket'} || $Ticket->CurrentUserHasRight('TakeTicket');
        } elsif ( $Ticket->Owner != $session{CurrentUser}->id ) {
            $actions->{'E'} = {
                path  => "RTIR/Display.html?Action=Steal&id=$id",
                title => loc('Steal')
            } if $can{'ModifyTicket'} || $Ticket->CurrentUserHasRight('StealTicket');
        }
    }

    my $i = 0;
    my $status = $Ticket->Status;
    my $lifecycle = $Ticket->QueueObj->lifecycle;
    foreach my $info ( $lifecycle->actions( $status ) ) {
        my $next = $info->{'to'};
        next unless $lifecycle->is_transition( $status => $next );

        my %args = (%$info, id => $id);
        delete @args{qw(to from label update)};

        # if user will be owner then he will get more rights
        # TODO: check actually if he will get required rights
        if ( !$args{'TakeOrStealFirst'} || $session{'CurrentUser'}->id == $Ticket->Owner ) {
            my $check = $lifecycle->check_right( $status => $next );
            $can{$check} = $Ticket->CurrentUserHasRight($check)
                unless exists $can{$check};
            next unless $can{$check};
        }

        my $path = 'RTIR/';
        $path .= 'Incident/' if $Type eq 'Incident';
        if ( my $update = $info->{'update'} ) {
            $path .= $Type eq 'Incident'? 'Reply.html' : 'Update.html';
            $path .= "?". $m->comp(
                '/Elements/QueryString',
                %args,
                Action        => $update,
                DefaultStatus => $next,
            );
        } else {
            $path .= "Display.html?". $m->comp(
                '/Elements/QueryString',
                %args,
                Status => $next,
            );
        }
        $actions->{ 'Ab'. $i++ } = {
            path  => $path,
            title => loc( $info->{'label'} || ucfirst($next) ),
        };
    }

    if ( $can{'ModifyTicket'} || $can{'CommentOnTicket'} ) {
        $actions->{'F'} = {
            title => loc('Comment'),
            path  => "RTIR/Update.html?Action=Comment&id=$id"
        };
    }

    $actions->{'_ZZ'} = {
        html => $m->scomp( '/Ticket/Elements/Bookmark', id => $id ),
    };

} else {
    $mytab = $current_tab;
}

$tabs->{"A"} = {
    path  => "RTIR/Create.html?$EscapedQueueName",
    title => loc("New $Type"),
};

$tabs->{"g"} = {
    path      => "RTIR/Search/Results.html?$EscapedQueueName&$QueryString",
    title     => loc('Results'),
};
$tabs->{"j"} = {
    path      => "RTIR/Search/Reporting.html?$EscapedQueueName&$QueryString",
    title     => loc('Report'),
};
unless ( $id ) {
    $tabs->{'ga'} = {
        class => "nav",
        path  => "RTIR/Search/Refine.html?$EscapedQueueName&$QueryString&"
            . $m->comp('/Elements/QueryString',
                BaseQuery  => $BaseQuery,
                ResultPage => "/RTIR/Search/Results.html?$EscapedQueueName",
            ),
        title => loc('Refine'),
    };
}


if ( $Type eq 'Report' ) {
    $tabs->{"h"} = {
        path      => "RTIR/Report/BulkReject.html?$EscapedQueueName&$QueryString",
        title     => loc('Bulk Reject'),
        separator => 1
    };
} elsif ( $Type eq 'Incident' ) {
    if ( $Ticket && $Ticket->QueueObj->Name ne 'Incidents' ) {
        my $childtype = RT::IR::TicketType( Ticket => $Ticket );

        $tabs->{'h'} = {
            title => loc( 'Link [_1] #[_2]', $childtype, $Ticket->Id ),
            path => "RTIR/Incident/LinkToIncident.html?id=" . $Ticket->Id,
            separator => 1
        };
    }
    $tabs->{'k'} = {
        path      => "RTIR/Incident/BulkAbandon.html?$EscapedQueueName&$QueryString",
        title     => loc('Bulk Abandon'),
        separator => 1
    };
}

if ( $subtabs && $current_tab ) {
    foreach my $tab ( values %{$tabs} ) {
        next unless $tab->{'path'} eq $current_tab;

        $tab->{'subtabs'} = { %{ $tab->{'subtabs'} || {} }, %$subtabs };
    }
}

</%INIT>
<%ARGS>
$Title          => ''
$id             => undef
$Ticket         => undef
$QueueName      => 'Incident Reports'

$BaseQuery      => ''
$QueryString    => ''

$appendtabs     => undef
$current_tab    => undef

$subtabs        => {}
$current_subtab => undef
</%ARGS>
